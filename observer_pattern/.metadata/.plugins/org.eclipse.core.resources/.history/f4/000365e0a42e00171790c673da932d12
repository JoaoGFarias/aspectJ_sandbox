package figures.observerPattern;

import java.util.HashMap;
import java.util.LinkedList;
import java.util.List;

import Observer;
import Subject;

public abstract aspect ObserverProcess {
	
	protected abstract pointcut subjectUpdate(Subject s);
	
	after(Subject subject) returning : subjectUpdate(subject) {
		
		for (Observer observer : getObservers(subject)) {
			updateObserver(subject, observer);
		}
		
	}
	
	protected HashMap<Subject,List<Observer>> subjectsObservers;
	
	protected abstract void updateObserver(Subject subject, Observer observer);
	
	public void addObserver(Subject subject, Observer observer) {
	    this.getObservers(subject).add(observer);
	}
	
	public void removeObserver(Subject subject, Observer observer) {
		this.getObservers(subject).remove(observer);
	}
	
	protected List<Observer> getObservers(Subject subject) {
	    
		if (this.subjectsObservers == null) {
	    	this.subjectsObservers = new HashMap<Subject,List<Observer>>();
	    }
	    
	    List<Observer> observers = this.subjectsObservers.get(subject);
	    
	    if ( observers == null ) {
	      observers = new LinkedList<Observer>();
	      this.subjectsObservers.put(subject, observers);
	    }
	    
	    return observers;
	  }
	
}
